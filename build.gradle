plugins {
    id "idea"
    id "maven-publish"
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

def generatedResources = file("src/generated")

sourceSets {
    main {
        resources.srcDir generatedResources
    }
}
repositories {
    mavenCentral()
    mavenLocal()
    maven { url = "https://maven.parchmentmc.org" }
    maven { url = "https://maven.resourcefulbees.com/repository/maven-public/" }
    maven {
        // Location of the maven that hosts Team Resourceful's jars.
        name = "Resourceful Bees Maven"
        url = "https://nexus.resourcefulbees.com/repository/maven-public/"
    }

    maven {
        url = 'https://maven.gtceu.com'
        content {
            includeGroup 'com.gregtechceu.gtceu'
        }
    }
    maven {

        url "https://maven.firstdarkdev.xyz/snapshots/"
    }
    maven {
        url "https://maven.latvian.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    }
    maven { // Registrate
        url = "https://maven.tterrag.com/"
        content {
            // need to be specific here due to version overlaps
            includeGroup "com.tterrag.registrate"
        }
    }
    maven {
        // Patchouli, JEI
        name = "BlameJared"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        // Curse Forge File
        url "https://cursemaven.com/"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        // EMI
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
    maven {
        url = "https://repo1.maven.org/maven2/io/github/llamalad7/"
        content {
            includeGroup "com.github.llamalad7"
        }
    }
    maven {
        url = "https://maven.createmod.net"
    }
    maven {
        url = "https://maven.architectury.dev"
    }
    /*maven {
        url = "https://thedarkcolour.github.io/KotlinForForge/"
    }*/
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
        // For Gradle 5.1 and above, limit it to just AE2
        content {
            includeGroup 'appeng'
        }
    }
    maven {
        name = 'GuideME Snapshots'
        url = 'https://central.sonatype.com/repository/maven-snapshots/'
        content {
            includeModule('org.appliedenergistics', 'guideme')
        }
    }
}
version = mod_version
group = maven_group
base { archivesName = archives_base_name }
java { sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17 }
sourceSets.main.resources { srcDir 'src/generated/resources' }

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods { "${mod_id}" { source sourceSets.main } }
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
        }
        client { property 'forge.enabledGameTestNamespaces', mod_id }
        server { property 'forge.enabledGameTestNamespaces', mod_id; args '--nogui' }
        data {
            // example of overriding the workingDirectory set in configureEach above
            workingDirectory project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            args '--existing-mod', 'gtceu'
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // JEI, EMI, Jade
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")
    runtimeOnly fg.deobf("dev.emi:emi-forge:${emi_version}+${minecraft_version}")
    implementation fg.deobf("curse.maven:jade-324717:5390389")

    // GregTech and dependencies
    runtimeOnly fg.deobf("com.gregtechceu.gtceu:gtceu-${project.minecraft_version}:${project.gtceu_version}:slim") {transitive = false}
    runtimeOnly fg.deobf("com.lowdragmc.ldlib:ldlib-forge-${minecraft_version}:${ldlib_version}") {transitive = false}
    implementation fg.deobf("com.tterrag.registrate:Registrate:${registrate_version}")
    compileOnly fg.deobf("com.gregtechceu.gtceu:gtceu-${project.minecraft_version}:${project.gtceu_version}:slim") {transitive = false}
    compileOnly fg.deobf("com.lowdragmc.ldlib:ldlib-forge-${minecraft_version}:${ldlib_version}") {transitive = false}
    runtimeOnly fg.deobf("dev.latvian.mods:kubejs-forge:${kubejs_version}")
    runtimeOnly fg.deobf("dev.latvian.mods:rhino-forge:${rhino_version}")
    compileOnly fg.deobf("dev.latvian.mods:kubejs-forge:${kubejs_version}")
    compileOnly fg.deobf("dev.latvian.mods:rhino-forge:${rhino_version}")
    compileOnly fg.deobf("earth.terrarium.adastra:ad_astra-forge-${minecraft_version}:${ad_astra_version}")
    implementation fg.deobf("com.teamresourceful.resourcefullib:resourcefullib-forge-${minecraft_version}:2.1.29")
    runtimeOnly fg.deobf("earth.terrarium.adastra:ad_astra-forge-${minecraft_version}:${ad_astra_version}") {transitive = false}
    runtimeOnly fg.deobf("com.teamresourceful.resourcefulconfig:resourcefulconfig-forge-${minecraft_version}:2.1.3")
    runtimeOnly fg.deobf("earth.terrarium.botarium:botarium-forge-${project.minecraft_version}:${project.botarium_version}")
    implementation(fg.deobf("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false })
    implementation(fg.deobf("net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_version}"))
    compileOnly(fg.deobf("dev.engine-room.flywheel:flywheel-forge-api-${minecraft_version}:${flywheel_version}"))
    runtimeOnly(fg.deobf("dev.engine-room.flywheel:flywheel-forge-${minecraft_version}:${flywheel_version}"))
    runtimeOnly fg.deobf("dev.toma.configuration:configuration-forge-${minecraft_version}:${configuration_version}")
    runtimeOnly fg.deobf("dev.architectury:architectury-forge:${architectury_version}")
    runtimeOnly fg.deobf("appeng:appliedenergistics2-forge:${ae2_version}")
    compileOnly fg.deobf("appeng:appliedenergistics2-forge:${ae2_version}")
    compileOnly fg.deobf('org.appliedenergistics:guideme:20.1.14:api')
    runtimeOnly fg.deobf('org.appliedenergistics:guideme:20.1.14')

//    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
//    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.5.0")) {
//        jarJar.ranged(it, "[0.5.0,)")
//    }

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    //mixin
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    // MixinGradle Settings
    add sourceSets.main, 'gttcore.mixins.refmap.json'
    config 'gttcore.mixins.json'
}

tasks.named('processResources', ProcessResources).configure {
    var properties = [
            "mod_license": mod_license,
            "mod_id": mod_id,
            "version": version,
            "mod_name": mod_name,
            "mod_url": mod_url,
            "mod_author": mod_author,
            "forge_version": forge_version.split("\\.")[0], // only specify major version of forge
            "minecraft_version": minecraft_version,
            "gtceu_version": gtceu_version,
            "ad_astra_version": ad_astra_version,
            "create_version": create_version
    ]
    inputs.properties(properties)

    filesMatching("META-INF/mods.toml") {
        expand properties + [project: project]
    }
}

tasks.named('jar', Jar).configure { finalizedBy 'reobfJar' }
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    options.encoding = "UTF-8"
    options.release.set(17)
}

//The executeGame.bat was hidden
tasks.register('runModpack') {
    dependsOn jar
    doLast {
        exec {
            commandLine "executeGame.bat", "${mod_id}-${mod_version}.jar"
        }
    }
}